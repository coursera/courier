buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:2.2.5'
  }
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'artifactory'

tasks.withType(Javadoc) {
  if (JavaVersion.current().isJava8Compatible()) {
    options.addStringOption('Xdoclint:none', '-quiet')
  }
}

repositories {
  mavenCentral()
  mavenLocal()
}

group = "org.coursera.courier"
archivesBaseName = "courier-gradle-plugin"
version = courierVersion

dependencies {
  compile "org.coursera.courier:courier-generator-api:$courierVersion"
  compile gradleApi()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

artifactoryPublish {
  contextUrl = artifactoryUrl

  publish {
    repository {
      repoKey = version.endsWith('SNAPSHOT') ? artifactoryPublishRepoKey + "-snapshots" : artifactoryPublishRepoKey
      username = artifactoryUsername
      password = artifactoryPassword
      ivy {
          mavenCompatible = true
      }
    }
  }
  resolve {
    repository {
      repoKey = artifactoryResolverRepoKey
      username = artifactoryUsername
      password = artifactoryPassword
      ivy {
          mavenCompatible = true
      }
    }
  }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Courier Gradle Plugin'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Scala data binding generator for multiple data formats including JSON and Avro.'
                url 'https://github.com/coursera/courier'

                scm {
                    connection 'git@github.com:coursera/courier.git'
                    developerConnection 'git@github.com:coursera/courier.git'
                    url 'https://github.com/coursera/courier/tree/master'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'jpbetz'
                        name 'Joe Betz'
                        email 'jpbetz@gmail.com'
                    }
                }
            }
        }
    }
}
